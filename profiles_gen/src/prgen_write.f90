!---------------------------------------------------------
! prgen_write.f90
!
! PURPOSE:
!  Generate input.profiles and input.profiles.extra
!----------------------------------------------------------

subroutine prgen_write

  use prgen_globals
  use expro

  !---------------------------------------------------------------
  implicit none
  !
  integer :: i
  !---------------------------------------------------------------

  open(unit=1,file='input.gacode',status='replace')

  !---------------------------------------------------------------
  ! Basic information
  !
  write(1,'(a,a)')   '# Generated by profiles_gen : ',trim(date)
  write(1,'(a,a)')   '#                INPUT FILE : ',trim(raw_data_file)
  write(1,'(a,a)')   '#           MERGED CER FILE : ',trim(cer_file)

  write(1,'(a)')     '#                      IONS :  Name       Z    Mass'
  do i=1,expro_n_ion
     write(1,'(a,t32,a,t41,i3,t47,f5.1,t53,a)') '#',&
          expro_name(i),nint(expro_z(i)),expro_mass(i),expro_type(i)
  enddo
  write(1,'(a,i2)')  '#                     IPCCW : ',ipccw
  write(1,'(a,i2)')  '#                     BTCCW : ',btccw

  !---------------------------------------------------------------
  ! High-resolution geometry
  !
  if (efit_method > 1) then
     write(1,'(a)') '# * An associated input.gacode.geo was generated'
     write(1,'(a)') '#'
     if ((format_type == 1 .or. format_type == 2)) then
        if (abs(dpsi_efit/dpsi_data-1) > 0.001) then
           write(1,'(a,1pe9.2,a)') &
                '#    FLUX SHRINK FACTOR : ',dpsi_efit/dpsi_data-1.0,' [WARNING]'
        else
           write(1,'(a,1pe9.2,a)') '#   FLUX SHRINK FACTOR : ',dpsi_efit/dpsi_data-1.0,' [GOOD]'
        endif
     endif
     write(1,40) '#             NFOURIER : ',nfourier
     write(1,40) '#                NSURF : ',nsurf
     write(1,40) '#                 NARC : ',narc
     if (efit_method /= 4) then
        write(1,20) '#            EFIT HEADER ... '
        write(1,20) '#'
        write(1,20) '#',efit_header
     endif
     write(1,20) '#'
  endif
  !---------------------------------------------------------------


  ! COORDINATES: For all data sources, ensure correct sign of expro_b_ref
  !              (and thus toroidal flux)
  !
  expro_rvbv  = 0.0
  expro_ip_exp = 0.0

  select case (format_type)

  case (0)
     ! (nothing but gfile)
     expro_torfluxa = -btccw*abs(expro_torfluxa)
  case (1)
     ! onetwo statefile
     expro_torfluxa = -0.5*btccw*abs(onetwo_Btor)*onetwo_rho_grid(nx)**2
     expro_rvbv  = onetwo_R0*onetwo_Btor
     expro_ip_exp = -ipccw*abs(ip_tot)
  case (2)
     ! plasmastate 
     expro_torfluxa = -btccw*abs(plst_phit(nx))/(2*pi)
  case (3)
     ! pfile
     expro_torfluxa = -btccw*abs(peqdsk_torfluxa)
  case (5)
     ! corsica
     expro_torfluxa = -btccw*abs(corsica_torfluxa)
  case (6)
     ! ufile
     expro_torfluxa = -btccw*abs(ufile_torfluxa)
  case (7)
     ! ufile
     expro_torfluxa = -btccw*abs(expro_torfluxa)
  end select

  !-------------------------------------------------------------------------------------
  ! Now we need to write the profile data
  !
  call expro_write('input.gacode')
  print '(a)','INFO: (prgen_write) Wrote input.gacode.'
  !-------------------------------------------------------------------------------------

20 format(5(a))
40 format(a,i6)

end subroutine prgen_write

