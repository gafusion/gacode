!---------------------------------------------------------
! prgen_write.f90
!
! PURPOSE:
!  Generate input.profiles and input.profiles.extra
!----------------------------------------------------------

subroutine prgen_write

  use prgen_globals
  use vpro

  !---------------------------------------------------------------
  implicit none
  !
  integer :: i,ip
  character (len=6) :: iname ! 6 character ion string
  character(len=46) :: header = '#                 IONS :  Name       Z    Mass'
  !---------------------------------------------------------------

  open(unit=1,file='input.gacode',status='replace')

  !---------------------------------------------------------------
  ! Basic information
  !
  write(1,'(a,a)')        '# Generated by profiles_gen : ',trim(date)
  write(1,'(a,a)')        '#                INPUT FILE : ',trim(raw_data_file)
  write(1,'(a,1pe8.2,a)') '#       PLASMA MAJOR RADIUS : ',rmaj(nx),' m'
  write(1,'(a,1pe8.2,a)') '#       PLASMA MINOR RADIUS : ',rmin(nx),' m'
  write(1,'(a,i2)')       '#                     IPCCW : ',ipccw
  write(1,'(a,i2)')       '#                     BTCCW : ',btccw
  !---------------------------------------------------------------
  if (cer_file /= "null") then
     write(1,'(a,a)')     '#           MERGED CER FILE : ',trim(cer_file)
  endif

  write(1,'(a)')          '#                      IONS :  Name       Z    Mass'
  do i=1,expro_n_ion
     write(1,'(a,t32,a,t41,i3,t47,f5.1,t53,a)') '#',&
          expro_name(i),nint(expro_z(i)),expro_mass(i),expro_type(i)
  enddo

  !---------------------------------------------------------------
  ! High-resolution geometry
  !
  if (efit_method > 1) then
     write(1,'(a)') '# * An associated input.profiles.geo was generated'
     write(1,'(a)') '#'
     if ((format_type == 1 .or. format_type == 2)) then
        if (abs(dpsi_efit/dpsi_data-1) > 0.001) then
           write(1,'(a,1pe9.2,a)') &
                '#    FLUX SHRINK FACTOR : ',dpsi_efit/dpsi_data-1.0,' [WARNING]'
        else
           write(1,'(a,1pe9.2,a)') '#   FLUX SHRINK FACTOR : ',dpsi_efit/dpsi_data-1.0,' [GOOD]'
        endif
     endif
     write(1,40) '#             NFOURIER : ',nfourier
     write(1,40) '#                NSURF : ',nsurf
     write(1,40) '#                 NARC : ',narc
     if (efit_method /= 4) then
        write(1,20) '#            EFIT HEADER ... '
        write(1,20) '#'
        write(1,20) '#',efit_header
     endif
     write(1,20) '#'
  endif
  !---------------------------------------------------------------


  ! COORDINATES: For all data sources, ensure correct sign of EXPRO_b_ref
  !              (and thus toroidal flux)
  !
  select case (format_type)

  case (0)
     ! (nothing but gfile)
     EXPRO_b_ref = -btccw*abs(null_bref)
     EXPRO_arho  = null_arho
     EXPRO_rvbv  = 0.0
     EXPRO_ip_exp = 0.0
  case (1)
     ! onetwo statefile
     EXPRO_b_ref = -btccw*abs(onetwo_Btor)
     EXPRO_arho  = onetwo_rho_grid(nx)
     EXPRO_rvbv  = onetwo_R0*onetwo_Btor
     EXPRO_ip_exp = -ipccw*abs(ip_tot)
  case (2)
     ! plasmastate 
     EXPRO_b_ref = -btccw*abs(plst_b_axis_vac)
     EXPRO_arho  = sqrt(plst_phit(nx)/plst_b_axis_vac/pi)
     EXPRO_rvbv  = 0.0
     EXPRO_ip_exp = 0.0
  case (3)
     ! pfile
     EXPRO_b_ref = -btccw*abs(peqdsk_bref)
     EXPRO_arho  = peqdsk_arho
     EXPRO_rvbv  = 0.0
     EXPRO_ip_exp = 0.0
  case (5)
     ! corsica
     EXPRO_b_ref = -btccw*abs(corsica_bref)
     EXPRO_arho  = corsica_arho
     EXPRO_rvbv  = 0.0
     EXPRO_ip_exp = 0.0
  case (6)
     ! ufile
     EXPRO_b_ref = -btccw*abs(ufile_bref)
     EXPRO_arho  = ufile_arho
     EXPRO_rvbv  = 0.0
     EXPRO_ip_exp = 0.0
  end select

  !-------------------------------------------------------------------------------------
  ! Now we need to write the profile data
  !
  call vpro_write
  print '(a)','INFO: (prgen_write) Wrote input.profiles.'
  !-------------------------------------------------------------------------------------

20 format(5(a))
40 format(a,i6)

end subroutine prgen_write
