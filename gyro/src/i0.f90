SUBROUTINE CALCI0(ARG,RESULT,JINT)

  !--------------------------------------------------------------------
  !     
  !     This packet computes modified Bessel functions of the first kind
  !     and order zero, I0(X) and EXP(-ABS(X))*I0(X), for real
  !     arguments X.  It contains two function type subprograms, BESI0
  !     and BESEI0, and one subroutine type subprogram, CALCI0.
  !     The calling statements for the primary entries are 
  !     
  !     Y=BESI0(X)
  !     and
  !     Y=BESEI0(X)
  !     
  !     where the entry points correspond to the functions I0(X) and
  !     EXP(-ABS(X))*I0(X), respectively.  The routine CALCI0 is
  !     intended for internal packet use only, all computations within
  !     the packet being concentrated in this routine.  The function
  !     subprograms invoke CALCI0 with the statement
  !     CALL CALCI0(ARG,RESULT,JINT)
  !     where the parameter usage is as follows
  !     
  !     Function                     Parameters for CALCI0
  !     Call              ARG                  RESULT          JINT
  !     
  !     BESI0(ARG)    ABS(ARG) .LE. XMAX        I0(ARG)           1
  !     BESEI0(ARG)    any real ARG        EXP(-ABS(ARG))*I0(ARG) 2
  !     
  !     The main computation evaluates slightly modified forms of
  !     minimax approximations generated by Blair and Edwards, Chalk
  !     River (Atomic Energy of Canada Limited) Report AECL-4928,
  !     October, 1974.  This transportable program is patterned after 
  !     the machine-dependent FUNPACK packet NATSI0, but cannot match
  !     that version for efficiency or accuracy.  This version uses
  !     rational functions that theoretically approximate I-SUB-0(X)
  !     to at least 18 significant decimal digits.  The accuracy
  !     achieved depends on the arithmetic system, the compiler, the
  !     intrinsic functions, and proper selection of the machine-
  !     dependent constants.
  !     
  !*******************************************************************
  !*******************************************************************
  !     
  !     Explanation of machine-dependent constants
  !     
  !     beta   = Radix for the floating-point system
  !     maxexp = Smallest power of beta that overflows
  !     XSMALL = Positive argument such that 1.0 - X = 1.0 to
  !     machine precision for all ABS(X) .LE. XSMALL.
  !     XINF =   Largest positive machine number; approximately
  !     beta**maxexp
  !     XMAX =   Largest argument acceptable to BESI0;  Solution to
  !     equation:  
  !     W(X) * (1+1/(8*X)+9/(128*X**2) = beta**maxexp
  !     where  W(X) = EXP(X)/SQRT(2*PI*X)
  !     
  !     
  !     Approximate values for some important machines are:
  !     
  !     beta       maxexp       XSMALL
  !     
  !     CRAY-1        (S.P.)       2         8191       3.55E-15
  !     Cyber 180/855
  !     under NOS   (S.P.)       2         1070       3.55E-15
  !     IEEE (IBM/XT,
  !     SUN, etc.)  (S.P.)       2          128       2.98E-8
  !     IEEE (IBM/XT,
  !     SUN, etc.)  (D.P.)       2         1024       5.55D-17
  !     IBM 3033      (D.P.)      16           63       6.95D-18
  !     VAX           (S.P.)       2          127       2.98E-8
  !     VAX D-Format  (D.P.)       2          127       6.95D-18
  !     VAX G-Format  (D.P.)       2         1023       5.55D-17
  !     
  !     
  !     XINF          XMAX
  !     
  !     CRAY-1        (S.P.)       5.45E+2465     5682.810
  !     Cyber 180/855
  !     under NOS   (S.P.)       1.26E+322       745.893
  !     IEEE (IBM/XT,
  !     SUN, etc.)  (S.P.)       3.40E+38         91.900
  !     IEEE (IBM/XT,
  !     SUN, etc.)  (D.P.)       1.79D+308       713.986
  !     IBM 3033      (D.P.)       7.23D+75        178.182
  !     VAX           (S.P.)       1.70D+38         91.203
  !     VAX D-Format  (D.P.)       1.70D+38         91.203
  !     VAX G-Format  (D.P.)       8.98D+307       713.293
  !     
  !*******************************************************************
  !*******************************************************************
  !     
  !     Error returns
  !     
  !     The program returns XINF for BESI0 for ABS(ARG) .GT. XMAX.
  !     
  !     
  !     Intrinsic functions required are:
  !     
  !     ABS, SQRT, EXP
  !     
  !     
  !     Authors: W. J. Cody and L. Stoltz
  !     Mathematics and Computer Science Division
  !     Argonne National Laboratory
  !     Argonne, IL 60439
  !     
  !     Latest modification: June 7, 1988
  !     
  !--------------------------------------------------------------------
  INTEGER :: I,JINT
  REAL :: A,ARG,B,EXP40,FORTY,ONE,ONE5,P,PP,Q,QQ,RESULT, &
       REC15,SUMP,SUMQ,TWO25,X,XINF,XMAX,XSMALL,XX

  DIMENSION P(15),PP(8),Q(5),QQ(7)
  !--------------------------------------------------------------------
  !     Mathematical constants
  !--------------------------------------------------------------------
  DATA ONE/1.0/,ONE5/15.0/,EXP40/2.353852668370199854e17/, &
       FORTY/40.0/,REC15/6.6666666666666666666e-2/, &
       TWO25/225.0/
  !--------------------------------------------------------------------
  !     Machine-dependent constants
  !--------------------------------------------------------------------
  DATA XSMALL/5.55e-17/,XINF/1.79e308/,XMAX/713.986/
  !--------------------------------------------------------------------
  !     Coefficients for XSMALL .LE. ABS(ARG) .LT. 15.0
  !--------------------------------------------------------------------
  DATA  P/-5.2487866627945699800D-18,-1.5982226675653184646D-14, &
       -2.6843448573468483278D-11,-3.0517226450451067446D-08,&
       -2.5172644670688975051D-05,-1.5453977791786851041D-02,&
       -7.0935347449210549190D+00,-2.4125195876041896775D+03,&
       -5.9545626019847898221D+05,-1.0313066708737980747D+08,&
       -1.1912746104985237192D+10,-8.4925101247114157499D+11,&
       -3.2940087627407749166D+13,-5.5050369673018427753D+14,&
       -2.2335582639474375249D+15/
  DATA  Q/-3.7277560179962773046D+03, 6.5158506418655165707D+06, &
       -6.5626560740833869295D+09, 3.7604188704092954661D+12, &
       -9.7087946179594019126D+14/
  !--------------------------------------------------------------------
  !     Coefficients for 15.0 .LE. ABS(ARG)
  !--------------------------------------------------------------------
  DATA PP/-3.9843750000000000000e-01, 2.9205384596336793945e+00,&
       -2.4708469169133954315e+00, 4.7914889422856814203e-01,&
       -3.7384991926068969150e-03,-2.6801520353328635310e-03,&
       9.9168777670983678974e-05,-2.1877128189032726730e-06/
  DATA QQ/-3.1446690275135491500e+01, 8.5539563258012929600e+01,&
       -6.0228002066743340583e+01, 1.3982595353892851542e+01,&
       -1.1151759188741312645e+00, 3.2547697594819615062e-02,&
       -5.5194330231005480228e-04/
  !--------------------------------------------------------------------
  X = ABS(ARG)
  IF (X < XSMALL) THEN
     RESULT = ONE
  ELSE IF (X < ONE5) THEN
     !--------------------------------------------------------------------
     !     XSMALL .LE.  ABS(ARG)  .LT. 15.0
     !--------------------------------------------------------------------
     XX = X * X
     SUMP = P(1)
     DO I = 2, 15
        SUMP = SUMP * XX + P(I)
     enddo
     XX = XX - TWO25
     SUMQ = ((((XX+Q(1))*XX+Q(2))*XX+Q(3))*XX+Q(4))*XX+Q(5)
     RESULT = SUMP / SUMQ
     IF (JINT == 2) RESULT = RESULT * EXP(-X)
  ELSE IF (X >= ONE5) THEN
     IF ((JINT .EQ. 1) .AND. (X .GT. XMAX)) THEN
        RESULT = XINF
     ELSE
        !--------------------------------------------------------------------
        !     15.0  .LE.  ABS(ARG)
        !--------------------------------------------------------------------
        XX = ONE / X - REC15
        SUMP = ((((((PP(1)*XX+PP(2))*XX+PP(3))*XX+PP(4))*XX+ &
             PP(5))*XX+PP(6))*XX+PP(7))*XX+PP(8)
        SUMQ = ((((((XX+QQ(1))*XX+QQ(2))*XX+QQ(3))*XX+ &
             QQ(4))*XX+QQ(5))*XX+QQ(6))*XX+QQ(7)
        RESULT = SUMP / SUMQ
        IF (JINT == 2) THEN
           RESULT = (RESULT - PP(1)) / SQRT(X)
        ELSE
           !--------------------------------------------------------------------
           !     Calculation reformulated to avoid premature overflow
           !--------------------------------------------------------------------
           IF (X <= (XMAX-ONE5)) THEN
              A = EXP(X)
              B = ONE
           ELSE
              A = EXP(X-FORTY)
              B = EXP40
           END IF
           RESULT = ((RESULT*A-PP(1)*A)/SQRT(X))*B
        END IF
     END IF
  END IF
  !--------------------------------------------------------------------
  !     Return for ABS(ARG) .LT. XSMALL
  !--------------------------------------------------------------------
  RETURN
  
END SUBROUTINE CALCI0

REAL FUNCTION BESI0(X)

  !--------------------------------------------------------------------
  !     
  !     This long precision subprogram computes approximate values for
  !     modified Bessel functions of the first kind of order zero for
  !     arguments ABS(ARG) .LE. XMAX  (see comments heading CALCI0).
  !     
  !--------------------------------------------------------------------

  INTEGER :: JINT
  REAL :: X, RESULT

  JINT=1
  CALL CALCI0(X,RESULT,JINT)
  BESI0=RESULT
  RETURN

END FUNCTION BESI0

REAL FUNCTION BESEI0(X)
  
  !--------------------------------------------------------------------
  !     
  !     This function program computes approximate values for the
  !     modified Bessel function of the first kind of order zero
  !     multiplied by EXP(-ABS(X)), where EXP is the
  !     exponential function, ABS is the absolute value, and X
  !     is any argument.
  !     
  !--------------------------------------------------------------------

  INTEGER :: JINT
  REAL :: X, RESULT

  JINT=2
  CALL CALCI0(X,RESULT,JINT)
  BESEI0=RESULT
  RETURN

END FUNCTION BESEI0
