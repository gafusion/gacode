dnl ######################################################################
dnl
dnl File:	tx_qt4.m4
dnl
dnl Purpose:	Determine the locations of QT includes and libraries
dnl
dnl Version:	$Id$
dnl
dnl Tech-X configure system
dnl
dnl ######################################################################

dnl Required for TX_PATH_FILE(S) to work
builtin(include, config/txsearch.m4)

dnl ######################################################################
dnl
dnl find the location of the qt installation
dnl
dnl ######################################################################

if test "$QT_DOSEARCH" != no; then
dnl always use threads
  AC_DEFINE(QT_THREAD_SUPPORT, , Whether to have threads in QT)

  case `uname` in
    Darwin)
      ;;
    Linux)
dnl See if qt is installed in the system
      AC_MSG_CHECKING(whether system has qt installed)
      AC_LANG_PUSH(C++)
      AC_TRY_COMPILE([#include <Qt/qapplication.h>], "",
        ac_cv_system_qt=yes,
        ac_cv_system_qt=no)
      AC_LANG_POP(C++)
      AC_MSG_RESULT($ac_cv_system_qt)
      if test $ac_cv_system_qt = yes; then
        QT_INCDIR=/usr/include
        AC_PATH_PROG(QTMOC, moc, "", $PATH:$QT_DIR/bin:/usr/lib64/qt4/bin)
        QT_LIBS="-lQtCore -lQtGui -lQt3Support -lQtOpenGL -lQtXml"
        AC_SUBST(QT_LIBS)
        QT_BINDIR=`dirname $QTMOC`
        QT_DIR=`dirname $QT_BINDIR`
        AC_SUBST(QT_DIR)
        QT_IMAGEFMTSDIR=$QT_DIR/plugins/imageformats
        AC_SUBST(QT_IMAGEFMTSDIR)
        QT_IMAGEFMTSLIBS="-L$QT_IMAGEFMTSDIR -${RPATH_FLAG}$QT_IMAGEFMTSDIR -lqgif -lqmng"
        AC_SUBST(QT_IMAGEFMTSLIBS)
        echo >> "$config_summary_file"
        echo Using system QT >> "$config_summary_file"
        TX_PRINT_VAR(QT_DIR)
        TX_PRINT_VAR(QT_INCDIR)
        TX_PRINT_VAR(QT_LIBS)
        TX_PRINT_VAR(QTMOC)
        TX_PRINT_VAR(QT_IMAGEFMTSDIR)
        TX_PRINT_VAR(QT_IMAGEFMTSLIBS)
      else
        if test -n "$SUPRA_SEARCH_PATH"; then
          for i in `echo $SUPRA_SEARCH_PATH | tr ':' ' '`; do
            QT_PATH="$QT_PATH:$i/qt"
          done
        else
          echo "SUPRA_SEARCH_PATH not set!"
        fi
        TX_LOCATE_PKG(
          [qt],
          [$QT_PATH],
          [qapplication.h],
          [QtCore, QtGui, Qt3Support, QtOpenGL, QtXml],
          [include/Qt],
          [lib])
dnl Correct for QT structure
        if test -n "$QT_INCDIR"; then
          QT_INCDIR=`dirname $QT_INCDIR`
          QT_INC=-I$QT_INCDIR
        fi
        QT_DIR=`dirname $QT_INCDIR`
        AC_PATH_PROG(QTMOC, moc, "", $QT_DIR/bin)
        QT_IMAGEFMTSDIR=$QT_DIR/plugins/imageformats
        AC_SUBST(QT_IMAGEFMTSDIR)
        QT_IMAGEFMTSLIBS="$QT_IMAGEFMTSDIR/libqgif.a $QT_IMAGEFMTSDIR/libqmng.a"
        AC_SUBST(QT_IMAGEFMTSLIBS)
      fi
dnl Print out modifications
      if test -n "$config_summary_file"; then
        if test -n "$QT_INCDIR"; then
          echo "  "MODIFICATIONS:  >> "$config_summary_file"
          TX_PRINT_VAR(QT_INCDIR)
          TX_PRINT_VAR(QT_INC)
        fi
      fi
      ;;

  esac

else
  echo >> "$config_summary_file"
  echo Not using QT >> "$config_summary_file"
fi
AM_CONDITIONAL(HAVE_QT, test -n "$QT_LIBS")

dnl ######################################################################
dnl
dnl Check the QT version
dnl
dnl ######################################################################

if test "$QT_DOSEARCH" != no; then
if test -n "$QT_INCDIR"; then
  AC_MSG_CHECKING(Qt version in $QT_INCDIR/Qt/qglobal.h)
  INC_FILE=$QT_INCDIR/Qt/qglobal.h
  QT_VERSION=`grep '#define QT_VERSION_STR' $INC_FILE | sed 's/^.*QT_VERSION_STR//' | sed 's/\/.*$//' | tr -d [:space:]\"`
  AC_EGREP_CPP(good_oopicpro_qt_version,
    [#include <$INC_FILE>
     #if (( QT_VERSION >= 0x30200 ))
     good_oopicpro_qt_version
     #endif],
  is_good_oopicpro_qt_version=yes,
  is_good_oopicpro_qt_version=no)
  AC_MSG_RESULT($is_good_oopicpro_qt_version)
  if test "$is_good_oopicpro_qt_version" = "no" ; then
    AC_MSG_WARN(OopicPro needs the Qt version to be >= 3.2 for the QSplashScreen to function properly.  Current Qt version is $QT_VERSION.  Please upgrade to Qt 3.2 or higher!)
  fi
fi
fi

dnl ######################################################################
dnl
dnl Find the window libraries
dnl
dnl ######################################################################

if test "$QT_DOSEARCH" != no; then
  case "$host" in

    *-apple-darwin*)
      QT_GLLIBS="-framework OpenGL -framework AGL"
      QT_GUILIBS="-framework QuickTime -framework Carbon"
      ;;

    *)
      AC_PATH_X
      ;;

  esac
fi


