dnl ######################################################################
dnl
dnl File:	tx_licmsg.m4
dnl
dnl Purpose:	Compile with license manager if location specified.
dnl		Very simple because we insist that the license manager be
dnl		contained in a static library (otherwise easy to spoof) 
dnl		which will be linked to the application.
dnl
dnl Version:	$Id$
dnl
dnl Copyright 2009-2010, Tech-X Corporation.  This file may be freely
dnl distributed provided copyright statement remains in place.
dnl
dnl ######################################################################

# Allow user to specify txlicmgr dir. If nothing specified, then we don't 
# really do anything here. There is no default expectation of finding a
# license manager as there is with txbase, for example.

AC_ARG_WITH(
    [txlicmgr-dir],
    [AC_HELP_STRING([--with-txlicmgr-dir=<dir>],
    [location of Tech-X license manager library])],
    [TXLICMGR_DIR="$withval"])

# in the event of a build vs. an installation, allow the user to specify
# both the include and lib dirs...

AC_ARG_WITH(txlicmgr-incdir, AC_HELP_STRING([--with-txlicmgr-incdir=<dir>],
        [txlicmgr include directory]),
	TXLICMGR_INCDIR="$withval")

AC_ARG_WITH(txlicmgr-libdir, AC_HELP_STRING([--with-txlicmgr-libdir=<dir>],
  [txlicmgr library directory]),
  TXLICMGR_LIBDIR="$withval")

# If user specified an include dir, we set the include path to be only that
# dir, otherwise we set it to be the include dir in the specified license
# manager dir.

if test -n "$TXLICMGR_INCDIR"; then
  TXLICMGR_INCPATH=$TXLICMGR_INCDIR
  TXLICMGR_DIR=$TXLICMGR_INCDIR
else
  TXLICMGR_INCPATH=$TXLICMGR_DIR/include
fi

# If user specified a lib dir, we set the include path to be only that dir, 
# otherwise we set it to be the lib dir in the specified license manager dir.

if test -n "$TXLICMGR_LIBDIR"; then
  TXLICMGR_LIBPATH=$TXLICMGR_LIBDIR
  TXLICMGR_DIR=$TXLICMGR_LIBDIR
else
  TXLICMGR_LIBPATH=$TXLICMGR_DIR/lib
fi

# If something was specified, then we look for a statically-built license 
# manager library (we don't want the library to be linked dynamically because
# that would be a security risk.

if test -n "$TXLICMGR_DIR"; then
  TX_PATH_FILE(LIBTXLICMGR_A, libtxlicmgr.a, "", $TXLICMGR_LIBPATH)
  if test -n "$LIBTXLICMGR_A"; then
    TXLICMGR_LIBDIR=`dirname $LIBTXLICMGR_A`
    TXLICMGR_LIB="-ltxlicmgr"
  else
    AC_MSG_ERROR(Could not find libtxlicmgr.a at $TXLICMGR_LIBPATH)
  fi
  TX_PATH_FILE(TXLICMGR_H, TxLicenseMgr.h, "", $TXLICMGR_INCPATH)
  if test -n "$TXLICMGR_H"; then
    TXLICMGR_INCDIR=`dirname $TXLICMGR_H`
    AC_SUBST(TXLICMGR_DIR)
    AC_SUBST(TXLICMGR_LIB)
    AC_SUBST(TXLICMGR_LIBDIR)
    AC_SUBST(TXLICMGR_INCDIR)
    AC_DEFINE([HAVE_SECURITY], , [Define if license manager present.])
    if test -n "$config_summary_file"; then
      TX_PRINT_VAR(TXLICMGR_LIBDIR)
      TX_PRINT_VAR(TXLICMGR_INCDIR)
      TX_PRINT_VAR(TXLICMGR_LIB)
      TX_PRINT_VAR(TXLICMGR_DIR)
    fi
  else
    AC_MSG_ERROR(Could not find TxLicenseMgr.h at $TXLICMGR_INCPATH)
  fi
fi

AM_CONDITIONAL(HAVE_SECURITY, test -n "$TXLICMGR_DIR")

