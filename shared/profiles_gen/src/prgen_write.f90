!---------------------------------------------------------
! prgen_write.f90
!
! PURPOSE:
!  Generate input.profiles and input.profiles.extra
!----------------------------------------------------------

subroutine prgen_write

  use prgen_globals
  use EXPRO_interface

  !---------------------------------------------------------------
  implicit none
  !
  integer :: i,ip
  integer :: indx
  character (len=15) :: ion_string ! Up to 5 two letter ions
  character (len=3) :: iname ! 3 character ion string
  !---------------------------------------------------------------

  open(unit=1,file='input.profiles',status='replace')

  !---------------------------------------------------------------
  ! Basic information
  !
  write(1,20) '# input.profiles  - generated by profiles_gen on ',trim(date)
  write(1,20) '#'
  write(1,20) '# See https://fusion.gat.com/theory/input.profiles for complete documentation.'
  write(1,20) '#'
  write(1,20) '# * Data description:'
  write(1,20) '#'
  write(1,20) '#           INPUT FILE : ',trim(raw_data_file)
  write(1,30) '#    RADIAL GRIDPOINTS : ',nx

  select case (format_type)

  case (1)

     if (cer_file /= "null") then
        write(1,20) '#      MERGED CER FILE : ',trim(cer_file)
     endif
     write(1,40) '#          SHOT NUMBER : ',onetwo_ishot
     write(1,20) '#'
     write(1,20) '#                 IONS :  Name       Z    Mass'

     do i=1,min(onetwo_nion_tot,5)
        ip = reorder_vec(i)
        if (ip > 0) then
           call prgen_ion_name(nint(onetwo_m(ip)),nint(onetwo_z(ip)),iname)
           write(1,'(a,a,t36,i3,t43,f4.1,t48,"[",a,"]")') '#                         ',&
                iname,&
                nint(onetwo_z(ip)),&
                onetwo_m(ip),&
                onetwo_type(ip)
        endif
     enddo

  case (2)

     write(1,20) '#              TOKAMAK : ',trim(plst_tokamak_id)
     write(1,40) '#          SHOT NUMBER : ',plst_shot_number
     write(1,20) '#'
     write(1,20) '#                 IONS :  Name       Z   Mass'
     do i=1,5
        ip = reorder_vec(i)
        if (ip < plst_dp1_nspec_all) then
           write(1,'(a,a,t36,i3,t43,i3)') '#                         ',&
                trim(plst_all_name(ip+1)),&
                nint(plst_q_all(ip+1)/1.6022e-19),&
                nint(plst_m_all(ip+1)/1.6726e-27)

        endif
     enddo
     write(1,20) '#'
     write(1,'(a,f7.2)') '#       Quasineutrality error (%) ',quasi_err*100.0
     write(1,'(a,f7.2)') '#       e power balance error (%) ',pow_e_err*100.0
     write(1,'(a,f7.2)') '#       i power balance error (%) ',pow_i_err*100.0

  case (3)

     peqdsk_nion = 1+peqdsk_nimp+peqdsk_nbeams

     write(1,20) '#                 IONS :  Name       Z    Mass'

     do i=1,peqdsk_nion
        ip = reorder_vec(i)
        call prgen_ion_name(nint(peqdsk_m(ip)),nint(peqdsk_z(ip)),iname)
        write(1,'(a,a,t36,i3,t43,f4.1,t48,"[",a,"]")') '#                         ',&
             iname,&
             nint(peqdsk_z(ip)),&
             peqdsk_m(ip),&
             peqdsk_type(ip)
     enddo

  case (6)

     write(1,20) '#              TOKAMAK : ',ufile_tok
     write(1,20) '#          SHOT NUMBER : ',ufile_shot
     write(1,20) '#             TIME (s) : ',ufile_time

     write(1,20) '#                 IONS :  Name       Z    Mass'
     do i=1,ufile_nion
        ip = reorder_vec(i)
        call prgen_ion_name(nint(ufile_m(ip)),nint(ufile_z(ip)),iname)
        write(1,'(a,a,t36,i3,t43,f4.1,t48,"[",a,"]")') '#                         ',&
             iname,&
             nint(ufile_z(ip)),&
             ufile_m(ip),&
             ufile_type(ip)
     enddo

     write(1,20) '#'
     write(1,'(a,f6.2)') '#       Quasineutrality error (%) ',quasi_err*100.0

  case (7)

     write(1,40) '#          SHOT NUMBER : [DATA MODIFIED WITH GMERGE]'
     write(1,20) '#'
     write(1,'(10(a,1x))') '#                 IONS : ',trim(cer_file)

  end select
  write(1,20) '# '
  !---------------------------------------------------------------

  write(1,20) '# * An associated input.profiles.extra was generated'
  write(1,20) '# '

  !---------------------------------------------------------------
  ! High-resolution geometry
  !
  if (efit_method > 1) then
     write(1,20) '# * An associated input.profiles.geo was generated'
     write(1,20) '#'
     if ((format_type == 1 .or. format_type == 2)) then
        if (abs(dpsi_efit/dpsi_data-1) > 0.001) then
           write(1,'(a,1pe9.2,a)') &
                '#    FLUX SHRINK FACTOR : ',dpsi_efit/dpsi_data-1.0,' [WARNING]'
        else
           write(1,'(a,1pe9.2,a)') '#   FLUX SHRINK FACTOR : ',dpsi_efit/dpsi_data-1.0,' [GOOD]'
        endif
     endif
     write(1,40) '#             NFOURIER : ',nfourier
     write(1,40) '#                NSURF : ',nsurf
     write(1,40) '#                 NARC : ',narc
     if (efit_method /= 4) then
        write(1,20) '#            EFIT HEADER ... '
        write(1,20) '#'
        write(1,20) '#',efit_header
     endif
     write(1,20) '#'
  endif
  !---------------------------------------------------------------

  !---------------------------------------------------------------
  ! Merged files and extra information
  !
  write(1,20) '# * Plasma dimensions:'
  write(1,20) '#'
  write(1,'(a,1pe8.2,a)') '#  PLASMA MAJOR RADIUS : ',rmaj(nx),' m'
  write(1,'(a,1pe8.2,a)') '#  PLASMA MINOR RADIUS : ',rmin(nx),' m'
  write(1,20) '#'
  write(1,20)    '# * Field/current orientation:'
  write(1,'(a)') '# '
  write(1,'(a)') '#   NOTE: All variables carry correct and consistent signs'
  write(1,'(a)') '#         in fixed gacode (r,theta,varphi) coordinates.'
  write(1,20) '#'
  write(1,'(a,i2)')       '#                IPCCW : ',ipccw
  write(1,'(a,i2)')       '#                BTCCW : ',btccw
  !---------------------------------------------------------------

  EXPRO_n_exp = nx

  ! COORDINATES: For all data sources, ensure correct sign of EXPRO_b_ref
  !              (and thus toroidal flux)
  !
  select case (format_type)

  case (0)
     EXPRO_b_ref = -btccw*abs(null_bref)
     EXPRO_arho  = null_arho
  case (1)
     EXPRO_b_ref = -btccw*abs(onetwo_Btor)
     EXPRO_arho  = onetwo_rho_grid(onetwo_nj)
  case (2)
     EXPRO_b_ref = -btccw*abs(plst_b_axis_vac)
     EXPRO_arho  = sqrt(plst_phit(nx)/plst_b_axis_vac/pi)
  case (3)
     EXPRO_b_ref = -btccw*abs(peqdsk_bref)
     EXPRO_arho  = peqdsk_arho
  case (5)
     EXPRO_b_ref = -btccw*abs(corsica_bref)
     EXPRO_arho  = corsica_arho
  case (6)
     EXPRO_b_ref = -btccw*abs(ufile_bref)
     EXPRO_arho  = ufile_arho
  end select

  write(1,20) '# '
  if (EXPRO_n_exp > 99) then
     write(1,30) 'N_EXP=',EXPRO_n_exp
  else
     write(1,25) 'N_EXP=',EXPRO_n_exp
  endif
  write(1,'(a,sp,1pe14.7)') 'BT_EXP=',EXPRO_b_ref
  write(1,60) 'ARHO_EXP=',EXPRO_arho

  do indx=1,n_indx,5

     write(1,20) '# '
     write(1,20) '#',EXPRO_tag(indx:indx+4)
     do i=1,nx
        write(1,10) vec(indx:indx+4,i)
     enddo

  enddo

  close(1)
  print '(a)','INFO: (prgen) Wrote input.profiles.'

  !-----------------------------------------------------------------------------------
  ! Define EXPRO interface variables, then compute and write derived quantities:
  !
  call EXPRO_alloc('./',1)

  EXPRO_rho(:)   = vec(1,:)
  EXPRO_rmin(:)  = vec(2,:)
  EXPRO_rmaj(:)  = vec(3,:)
  EXPRO_q(:)     = vec(4,:)
  EXPRO_kappa(:) = vec(5,:)

  EXPRO_delta(:) = vec(6,:)
  EXPRO_te(:)    = vec(7,:)
  EXPRO_ne(:)    = vec(8,:)
  EXPRO_z_eff(:) = vec(9,:)
  EXPRO_w0(:)    = vec(10,:)

  EXPRO_flow_mom(:) = vec(11,:)
  EXPRO_pow_e(:)    = vec(12,:)
  EXPRO_pow_i(:)    = vec(13,:)
  EXPRO_pow_ei(:)   = vec(14,:)
  EXPRO_zeta(:)     = vec(15,:)

  EXPRO_flow_beam(:) = vec(16,:)
  EXPRO_flow_wall(:) = vec(17,:)
  EXPRO_zmag(:)      = vec(18,:)
  EXPRO_ptot(:)      = vec(19,:)
  EXPRO_poloidalfluxover2pi(:) = vec(20,:)

  EXPRO_ni(1:5,:) = vec(21:25,:)

  EXPRO_ti(1:5,:) = vec(26:30,:)

  EXPRO_vtor(1:5,:) = vec(31:35,:)

  EXPRO_vpol(1:5,:) = vec(36:40,:)

  EXPRO_ctrl_density_method=1
  EXPRO_ctrl_z(1) = 1.0
  EXPRO_ctrl_rotation_method = 1
  if (efit_method > 1) then
     EXPRO_ctrl_numeq_flag = 1
     call EXPRO_read_geo
  else
     EXPRO_ctrl_numeq_flag = 0
  endif

  call EXPRO_compute_derived
  call EXPRO_write_derived
  print '(a)','INFO: (prgen) Wrote input.profiles.extra.'
  !-----------------------------------------------------------------------------------

10 format(5(1pe14.7,2x))
20 format(30(a))
25 format(a,i2)
30 format(a,i3)
40 format(a,i6)
60 format(a,1pe13.7)

end subroutine prgen_write
