!---------------------------------------------------------
! prgen_write.f90
!
! PURPOSE:
!  Generate input.profiles.
!----------------------------------------------------------

subroutine prgen_write

  use prgen_read_globals

  !---------------------------------------------------------------
  implicit none
  !
  integer :: i
  integer :: indx
  !---------------------------------------------------------------

  open(unit=1,file='input.profiles',status='replace')

  !---------------------------------------------------------------
  ! Basic information
  !
  write(1,20) '#'
  write(1,20) '# * Generated by profiles_gen on ',trim(date)
  write(1,20) '#'
  write(1,20) '#           INPUT FILE : ',trim(raw_data_file)

  select case (format_type)

  case (1)
     write(1,40) '#          SHOT NUMBER : ',onetwo_ishot
     write(1,30) '#    RADIAL GRIDPOINTS : ',nx
     write(1,'(a,1pe9.2,a)') '#               Q_EDGE : ',q(nx)
     write(1,20) '#'
     write(1,'(10(a,1x))') '#                 IONS : ',&
          (trim(ion_name(reorder_vec(i))),i=1,onetwo_nion+onetwo_nbion)

  case (2)
     write(1,20) '#              TOKAMAK : ',trim(plst_tokamak_id)
     write(1,40) '#          SHOT NUMBER : ',plst_shot_number
     write(1,30) '#    RADIAL GRIDPOINTS : ',nx
     write(1,'(a,1pe9.2,a)') '#               Q_EDGE : ',q(nx)
     write(1,20) '#'
     write(1,'(10(a,1x))') '#                 IONS :',&
          (trim(plst_alla_name(reorder_vec(i-1)+1)),&
          i=2,plst_dp1_nspec_th+1) ! WG added +1 for beam ion

  case (3)
     write(1,40) '#          SHOT NUMBER : '
     write(1,30) '#    RADIAL GRIDPOINTS : ',nx
     write(1,'(a,1pe9.2,a)') '#               Q_EDGE : ',q(nx)
     write(1,20) '#'
     write(1,20) '#                 IONS : ?'

  end select

  write(1,20) '# '
  if (signpsi > 0.0) then
     write(1,30) '#         SIGN(PSI_POL): +'
  else
     write(1,30) '#         SIGN(PSI_POL): -'
  endif
  write(1,20) '# '
  !---------------------------------------------------------------

  !---------------------------------------------------------------
  ! High-resolution geometry
  !
  if (gato_flag == 1) then
     write(1,20) '# * An associated input.profiles.geo was also generated'
     write(1,20) '#'
     write(1,40) '#             NFOURIER : ',nfourier
     write(1,40) '#            GATO NPSI : ',gato_npsi
     write(1,40) '#          GATO NTHETA : ',gato_ntheta
     write(1,20) '#            EFIT HEADER ... '
     write(1,20) '#'
     write(1,20) '#',efit_header
     write(1,20) '#'
     if (abs(dpsi_gato/dpsi_data-1) > 0.001) then
        write(1,'(a,1pe9.2,a)') &
             '# ** WARNING ** Psi-shrinkage factor : ',dpsi_gato/dpsi_data-1.0  
     else
        write(1,'(a,1pe9.2,a)') '# Psi-shrinkage factor : ',dpsi_gato/dpsi_data-1.0 
     endif
  endif
  !---------------------------------------------------------------

  !---------------------------------------------------------------
  ! Merged files and extra information
  !
  write(1,20) '# * Additional information:'
  write(1,20) '#'

  if (cer_file /= "null") then
     write(1,20) '#      MERGED CER FILE : ',trim(cer_file)
  endif

  write(1,'(a,1pe8.2,a)') '#  PLASMA MAJOR RADIUS : ',rmaj(nx),' m'
  write(1,'(a,1pe8.2,a)') '#  PLASMA MINOR RADIUS : ',rmin(nx),' m'
  write(1,20) '#'
  !---------------------------------------------------------------

  write(1,20) '# '
  if (nx > 99) then
     write(1,30) 'N_EXP=',nx
  else
     write(1,25) 'N_EXP=',nx
  endif
  if (format_type == 1) then
     write(1,'(a,sp1pe14.7)') 'BT_EXP=',-onetwo_Btor
     write(1,60) 'ARHO_EXP=',onetwo_rho_grid(onetwo_nj)
  else if (format_type == 2) then
     write(1,'(a,sp1pe14.7)') 'BT_EXP=',plst_b_axis_vac*(-plst_kccw_bphi)
     write(1,60) 'ARHO_EXP=',sqrt(plst_phit(nx)/plst_b_axis_vac/pi)
  else
     write(1,'(a,sp1pe14.7)') 'BT_EXP=',peqdsk_bref
     write(1,60) 'ARHO_EXP=',peqdsk_arho
  endif

  do indx=1,n_indx,5

     write(1,20) '# '
     write(1,20) '#',tag(indx:indx+4)
     do i=1,nx
        write(1,10) vec(indx:indx+4,i) 
     enddo

  enddo

  !----------------------------------------------------------------------
  ! Extra transport powers 
  !
  if (extra_powers_flag == 1) then

     do indx=1,n_indx2,5

        write(1,20) '# '
        write(1,20) '#',tag2(indx:indx+4)
        do i=1,nx
           write(1,10) vec2(indx:indx+4,i)
        enddo

     enddo

  endif
  !-----------------------------------------------------------------------

  close(1)

10 format(5(1pe14.7,2x))
20 format(30(a))
25 format(a,i2)
30 format(a,i3)
40 format(a,i6)
60 format(a,1pe13.7)

end subroutine prgen_write
