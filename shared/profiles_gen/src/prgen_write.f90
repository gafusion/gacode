!---------------------------------------------------------
! prgen_write.f90
!
! PURPOSE:
!  Generate input.profiles.
!----------------------------------------------------------

subroutine prgen_write

  use prgen_globals
  use EXPRO_interface

  !---------------------------------------------------------------
  implicit none
  !
  integer :: i
  integer :: indx
  !---------------------------------------------------------------


  open(unit=1,file='input.profiles',status='replace')

  !---------------------------------------------------------------
  ! Basic information
  !
  write(1,20) '# input.profiles  - generated by profiles_gen on ',trim(date)
  write(1,20) '#'
  write(1,20) '# See https://fusion.gat.com/theory/input.profiles for complete documentation.'
  write(1,20) '#'
  write(1,20) '#           INPUT FILE : ',trim(raw_data_file)

  select case (format_type)

  case (1)
     if (cer_file /= "null") then
        write(1,20) '#      MERGED CER FILE : ',trim(cer_file)
     endif
     write(1,40) '#          SHOT NUMBER : ',onetwo_ishot
     write(1,30) '#    RADIAL GRIDPOINTS : ',nx
     write(1,'(a,1pe9.2,a)') '#               Q_EDGE : ',q(nx)
     write(1,20) '#'
     write(1,'(10(a,1x))') '#                 IONS : ',&
          (trim(ion_name(reorder_vec(i))),i=1,onetwo_nion+onetwo_nbion)

  case (2)
     write(1,20) '#              TOKAMAK : ',trim(plst_tokamak_id)
     write(1,40) '#          SHOT NUMBER : ',plst_shot_number
     write(1,30) '#    RADIAL GRIDPOINTS : ',nx
     write(1,'(a,1pe9.2,a)') '#               Q_EDGE : ',q(nx)
     write(1,20) '#'
     write(1,'(10(a,1x))') '#                 IONS :',&
          (trim(plst_all_name(reorder_vec(i-1)+1)),&
          i=2,min(plst_dp1_nspec_th+1,6))

  case (3)
     write(1,40) '#          SHOT NUMBER : '
     write(1,30) '#    RADIAL GRIDPOINTS : ',nx
     write(1,'(a,1pe9.2,a)') '#               Q_EDGE : ',q(nx)
     write(1,20) '#'
     write(1,20) '#                 IONS : D [assumed]'

  case (6)
     write(1,20) '#              TOKAMAK : ',ufile_tok
     write(1,20) '#          SHOT NUMBER : ',ufile_shot
     write(1,20) '#             TIME (s) : ',ufile_time
     write(1,30) '#    RADIAL GRIDPOINTS : ',nx
     write(1,'(a,1pe9.2,a)') '#               Q_EDGE : ',q(nx)
     write(1,20) '#'

  case (7)
     write(1,40) '#          SHOT NUMBER : [DATA MODIFIED WITH GMERGE]'
     write(1,30) '#    RADIAL GRIDPOINTS : ',nx
     write(1,'(a,1pe9.2,a)') '#               Q_EDGE : ',q(nx)
     write(1,20) '#'
     write(1,'(10(a,1x))') '#                 IONS : ',trim(cer_file)

  end select

  if (format_type > 0 .and. format_type < 6) then
     write(1,20) '# '
     if (signpsi > 0.0) then
        write(1,30) '#         SIGN(PSI_POL): +'
     else
        write(1,30) '#         SIGN(PSI_POL): -'
     endif
  endif
  write(1,20) '# '
  !---------------------------------------------------------------

  write(1,20) '# * An associated input.profiles.extra was also generated'

  !---------------------------------------------------------------
  ! High-resolution geometry
  !
  if (efit_method > 0) then
     write(1,20) '# * An associated input.profiles.geo was also generated'
     write(1,20) '#'
     write(1,40) '#             NFOURIER : ',nfourier
     write(1,40) '#                NSURF : ',nsurf
     write(1,40) '#                 NARC : ',narc
     write(1,20) '#            EFIT HEADER ... '
     write(1,20) '#'
     write(1,20) '#',efit_header
     write(1,20) '#'
     if (format_type > 0 .and. format_type < 6) then
        if (abs(dpsi_efit/dpsi_data-1) > 0.001) then
           write(1,'(a,1pe9.2,a)') &
                '# ** WARNING ** Psi-shrinkage factor : ',dpsi_efit/dpsi_data-1.0  
        else
           write(1,'(a,1pe9.2,a)') '# Psi-shrinkage factor : ',dpsi_efit/dpsi_data-1.0 
        endif
     endif
  endif
  !---------------------------------------------------------------

  !---------------------------------------------------------------
  ! Merged files and extra information
  !
  write(1,20) '# * Additional information:'
  write(1,20) '#'

  write(1,'(a,1pe8.2,a)') '#  PLASMA MAJOR RADIUS : ',rmaj(nx),' m'
  write(1,'(a,1pe8.2,a)') '#  PLASMA MINOR RADIUS : ',rmin(nx),' m'
  write(1,20) '#'
  !---------------------------------------------------------------

  EXPRO_n_exp = nx

  select case (format_type)

  case (0)
     EXPRO_b_ref = null_bref
     EXPRO_arho  = null_arho
  case (1)
     EXPRO_b_ref = -onetwo_Btor
     EXPRO_arho  = onetwo_rho_grid(onetwo_nj)
  case (2)
     EXPRO_b_ref = plst_b_axis_vac*(-plst_kccw_bphi)
     EXPRO_arho  = sqrt(plst_phit(nx)/plst_b_axis_vac/pi)
  case (3)
     EXPRO_b_ref = peqdsk_bref
     EXPRO_arho  = peqdsk_arho
  case (5)
     EXPRO_b_ref = corsica_bref
     EXPRO_arho  = corsica_arho
  case (6)
     EXPRO_b_ref = ufile_bref
     EXPRO_arho  = ufile_arho
  end select

  write(1,20) '# '
  if (EXPRO_n_exp > 99) then
     write(1,30) 'N_EXP=',EXPRO_n_exp
  else
     write(1,25) 'N_EXP=',EXPRO_n_exp
  endif
  write(1,'(a,sp,1pe14.7)') 'BT_EXP=',EXPRO_b_ref
  write(1,60) 'ARHO_EXP=',EXPRO_arho

  do indx=1,n_indx,5

     write(1,20) '# '
     write(1,20) '#',EXPRO_tag(indx:indx+4)
     do i=1,nx
        write(1,10) vec(indx:indx+4,i) 
     enddo

  enddo

  close(1)

  !-----------------------------------------------------------------------------------
  ! Define EXPRO interface variables, then compute and write derived quantities:
  !
  call EXPRO_alloc('./',1)

  EXPRO_rho(:)   = vec(1,:)
  EXPRO_rmin(:)  = vec(2,:)
  EXPRO_rmaj(:)  = vec(3,:)
  EXPRO_q(:)     = vec(4,:)
  EXPRO_kappa(:) = vec(5,:)

  EXPRO_delta(:) = vec(6,:)
  EXPRO_te(:)    = vec(7,:)
  EXPRO_ne(:)    = vec(8,:)
  EXPRO_z_eff(:) = vec(9,:)
  EXPRO_w0(:)    = vec(10,:)

  EXPRO_flow_mom(:) = vec(11,:)
  EXPRO_pow_e(:)    = vec(12,:)
  EXPRO_pow_i(:)    = vec(13,:)
  EXPRO_pow_ei(:)   = vec(14,:)
  EXPRO_zeta(:)     = vec(15,:)

  EXPRO_flow_beam(:) = vec(16,:)
  EXPRO_flow_wall(:) = vec(17,:)
  EXPRO_zmag(:)      = vec(18,:)
  EXPRO_ptot(:)      = vec(19,:)
  EXPRO_poloidalfluxover2pi(:) = vec(20,:)

  EXPRO_ni(1:5,:) = vec(21:25,:)

  EXPRO_ti(1:5,:) = vec(26:30,:)

  EXPRO_vtor(1:5,:) = vec(31:35,:)

  EXPRO_vpol(1:5,:) = vec(36:40,:)

  EXPRO_ctrl_density_method=1
  EXPRO_ctrl_z(1) = 1.0 
  EXPRO_ctrl_signq = 1.0
  EXPRO_ctrl_signb = 1.0
  EXPRO_ctrl_rotation_method = 1
  if (efit_method > 1) then
     EXPRO_ctrl_numeq_flag = 1
     call EXPRO_read_geo
  else
     EXPRO_ctrl_numeq_flag = 0
  endif

  call EXPRO_compute_derived
  call EXPRO_write_derived
  call EXPRO_alloc('./',0)
  !-----------------------------------------------------------------------------------

10 format(5(1pe14.7,2x))
20 format(30(a))
25 format(a,i2)
30 format(a,i3)
40 format(a,i6)
60 format(a,1pe13.7)

end subroutine prgen_write
